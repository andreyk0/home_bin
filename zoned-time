#!/usr/bin/env stack
{- stack
   --resolver lts-13.12
   --install-ghc
   --silent
   runghc
   --package base
   --package optparse-applicative
   --package time
   --
   -hide-all-packages
-}


{-# LANGUAGE RecordWildCards  #-}


import           Control.Monad
import           Data.List
import           Data.Monoid
import           Data.Time
import           Options.Applicative


data Args = Args { argTz :: TimeZone
                 , argTimes :: [ZonedTime]
                 } deriving (Show)


parseArgs :: Parser Args
parseArgs = Args
     <$> option parseTimeZoneArg
         ( long "time-zone"
        <> short 'z'
        <> value utc
        <> showDefault
        <> help "Target time zone." )
     <*> many (argument parseTimeArg (metavar "ISO8601Dates..."))


parseTimeZoneArg :: ReadM TimeZone
parseTimeZoneArg = eitherReader $ \tzName ->
  let maybeTz = find (\tz -> (timeZoneName tz) == tzName) supportedZones
   in case maybeTz
        of Nothing -> Left $ "Unable to find time zone " <> tzName <> " in the list of supported zones " <> (show supportedZones)
           Just tz -> Right tz


parseTimeArg :: ReadM ZonedTime
parseTimeArg = eitherReader $ \tsStr ->
  case parseTimeM False defaultTimeLocale iso8601FmtStr tsStr
    of Nothing -> Left $ "failed to parse " <> tsStr <> " with " <> iso8601FmtStr
       Just t  -> Right t


supportedZones :: [TimeZone]
supportedZones = utc : (knownTimeZones defaultTimeLocale)

iso8601FmtStr:: String
iso8601FmtStr = "%Y-%m-%dT%H:%M:%S%Z"


main:: IO ()
main = execParser opts >>= runMain
  where
    opts = info (helper <*> parseArgs)
      ( fullDesc
     <> progDesc ("Converts given time to different time zone. Supported time zones: " <> (show supportedZones))
     <> header "Converts given time to different time zone." )



runMain :: Args
        -> IO ()
runMain Args{..} = do
  zonedNow <-getZonedTime
  forM_ (if null argTimes then [zonedNow] else argTimes) $ \zt -> do
    let zt' = utcToZonedTime argTz $ zonedTimeToUTC zt
    putStrLn $ formatTime defaultTimeLocale iso8601FmtStr zt'
